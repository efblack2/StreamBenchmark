#!/usr/bin/gnuplot
# call it as:
# gnuplot -c plot.gnp stout
#
machine=ARG1
test=ARG2

set terminal pdf enhanced
set output machine."-".test."Ratio.pdf"

plotTitle=machine."-".test

set style line 1 dt 1 ps 0.6 lc rgb "#ff0000";
set style line 2 dt 4 ps 0.5 lc rgb "#ff4f00";

set style line 3 dt 1 ps 0.6 lc rgb "#0000ff";
set style line 4 dt 4 ps 0.5 lc rgb "#0099ff";

set style line 5 dt 1 ps 0.6 lc rgb "#00ff00";
set style line 6 dt 4 ps 0.5 lc rgb "#00ff99";

set key bottom right box
set grid

set xlabel "Number of Processors/Threads"
set ylabel "Rate MB/s"
set xrange [0:]
set yrange [0:]
     
set title plotTitle
set label "Stream Benchmark - MPI\_{sm} to OpenMP Ratio" at graph 0.5, 1.035 center font "Arial,11"

plot machine.'/gnu/result.txt'   using 1:($2/$4) title "Gnu"   with linespoints ls 1,\
     machine.'/intel/result.txt' using 1:($2/$4) title "Intel" with linespoints ls 3,\
     machine.'/pgi/result.txt'   using 1:($2/$4) title "Pgi"     with linespoints ls 5 
     
