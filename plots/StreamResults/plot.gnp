#!/usr/bin/gnuplot
# call it as:
# gnuplot -c plot.gnp stout
#
machine=ARG1
test=ARG2

set terminal pdf enhanced
set output machine. "-".test.".pdf"

plotTitle=test." test in ".machine

set style line 1 dt 1 ps 0.6 lc rgb "#ff0000";
set style line 2 dt 4 ps 0.5 lc rgb "#ff4f00";

set style line 3 dt 1 ps 0.6 lc rgb "#0000ff";
set style line 4 dt 4 ps 0.5 lc rgb "#0099ff";

set style line 5 dt 1 ps 0.6 lc rgb "#00ff00";
set style line 6 dt 4 ps 0.5 lc rgb "#00ff99";

set key bottom right box
set grid

set xlabel "Number of Processors/Threads"
set ylabel "Rate MB/s"
set xrange [0:]
set yrange [0:]
     
set title "Stream Benchmark"
set label plotTitle at graph 0.5, 1.035 center font "Arial,11"

plot machine.'/gnu/result.txt'   using 1:2 title "Gnu MPI_{sm}"   with linespoints ls 1,\
     machine.'/gnu/result.txt'   using 3:4 title "Gnu OpenMP"     with linespoints ls 2,\
     machine.'/intel/result.txt' using 1:2 title "Intel MPI_{sm}" with linespoints ls 3,\
     machine.'/intel/result.txt' using 3:4 title "Intel OpenMP"   with linespoints ls 4,\
     machine.'/pgi/result.txt'   using 1:2 title "Pgi MPI_{sm}"   with linespoints ls 5,\
     machine.'/pgi/result.txt'   using 3:4 title "Pgi OpenMP"     with linespoints ls 6 
     
